#!/usr/bin/perl

#
# NethServer Samba
# Copyright (C) 2012 Nethesis srl
#

use strict;
use esmith::AccountsDB;

my $event = shift || die("Missing event argument!");
my $user= shift || die("No user specified");
my $accountsDb = esmith::AccountsDB->open() || die("Could not open accounts database");
my $record = $accountsDb->get($user) || die("User `$user` not found in accounts database");
my $exitCode = 0;
my $net = '/usr/bin/net';
my $smbpasswd = '/usr/bin/smbpasswd';

if($record->prop('type') ne 'user') {
    die("`$user` is not a user record");
}

# Update SAM db to reflect the "enabled" state of the user account:
{
    my $isEnabled;
    
    if($record->prop('Locked') ne 'yes' 
       && $record->prop('Samba') eq 'enabled') {
	$isEnabled = 1;
    } else {
	$isEnabled = 0;
    }
    
    if(system($smbpasswd, $isEnabled ? '-e' : '-d', $user) != 0) {
	warn "Failed to change enabled state of SAM user `$user`";
	$exitCode ++;
    }
}

# Update SAM user fullname and comment
{
    my $fullName = join(' ', $record->prop('FirstName'), $record->prop('LastName'));
    foreach my $attribute (qw(fullname comment)) {
	system($net, 'sam', 'set', $attribute, $user, $fullName);
	if($? != 0) {
	    warn "Failed to change full name of SAM user `$user`";
	    $exitCode ++;
	}
    }
} 

exit($exitCode == 0 ? 0 : 1);

