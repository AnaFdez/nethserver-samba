#!/usr/bin/perl

#
# NethServer Samba
#
# Create new user(s) in SAM database.
#
# The new user name is specified by the second argument to the script.
# If the user name value has not been set ALL users in AccountDB are processed.
#
# @copyright (C) 2012 Nethesis srl
#

use strict;
use esmith::AccountsDB;

my $event = shift;
my $user = shift;
my @users;
my $accountsDb = esmith::AccountsDB->open();

my $exitCode = 0;

my $pdbedit = '/usr/bin/pdbedit';
my $smbpasswd = '/usr/bin/smbpasswd';
my $net = '/usr/bin/net';

if($user) {
    if($accountsDb->get_prop($user, 'type') ne 'user') {
	die("Invalid user account name: `$user`");
    }
    @users = ($user);
} else {
    @users = map { $_->key } $accountsDb->users();   
}


foreach my $user (@users) {

    # Check if user exists in SAM:
    qx($pdbedit -L -u "$user" >/dev/null 2>&1);
    if($? == 0) {
	warn "Skipping existing SAM user `$user`";
	next;
    }

    # Add to SAM:
    qx($smbpasswd -a -n -d "$user" >/dev/null 2>&1);
    if($? != 0) {
	warn "Failed to add user `$user` in SAM database";
	$exitCode ++;
	next;
    }

    # Map user's unix group in SAM database
    qx($net sam mapunixgroup "$user");
    if($? != 0) {
	warn "Failed to map user's unix group `$user` in SAM database";
	$exitCode ++;
    }

    # Default user Samba state ( Refs #974 ):
    # active user => Samba disabled; 
    # new user => Samba enabled.
    my $userState = $accountsDb->get_prop($user, '__state');
    if ($userState eq 'active') {
	$accountsDb->set_prop($user, 'Samba', 'disabled');
    } elsif ($userState eq 'new') {
	$accountsDb->set_prop($user, 'Samba', 'enabled');
    }

    # Make sure the password is in expired state, to avoid valid empty passwords:
    if(system($net, qw(sam set pwdmustchangenow), $user, 'yes') != 0) {
	warn "Failed to set password state of SAM user `$user` to expired";
	$exitCode ++;
    }
}

exit(($exitCode > 0) ? 1 : 0);


